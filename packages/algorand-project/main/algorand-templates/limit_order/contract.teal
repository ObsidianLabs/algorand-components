#pragma version 2
txn GroupIndex
int 0
==
txn TypeEnum
int 1 // Payment
==
&&
txn Fee
int 10000
<=
&&

global GroupSize
int 1
==
bnz closeOut

global GroupSize
int 2
==
txn Amount
int 100000
>
&&
txn CloseRemainderTo
global ZeroAddress
==
&&
gtxn 1 TypeEnum
int 4 // AssetTransfer
==
&&
gtxn 1 XferAsset // Asset ID
int 12006640
==
&&
gtxn 1 AssetReceiver
addr E7LNST5WO4TPZHK7HPJ2XC3MX2U3EEUE3ZPQKWAB6FLDEKJRPQ3WIRDUF4
==
&&
gtxn 1 AssetSender
global ZeroAddress
==
&&

gtxn 1 AssetAmount
int 1000000
mulw
store 2 // Low 64 bits
store 1 // High 64 bits
txn Amount
int 10
mulw
store 4 // Low 64 bits
store 3 // High 64 bits
load 1
load 3
>
bnz done
load 1
load 3
==
load 2
load 4
>=
&&
bnz done
err

closeOut:
txn CloseRemainderTo
addr E7LNST5WO4TPZHK7HPJ2XC3MX2U3EEUE3ZPQKWAB6FLDEKJRPQ3WIRDUF4
==
txn FirstValid
int 8693064
>
&&
txn Receiver
global ZeroAddress
==
&&
txn Amount
int 0
==
&&

done:
&&
